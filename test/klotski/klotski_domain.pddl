(define (domain klotski)
(:requirements :strips)
(:predicates
	(at ?block ?pos)
	(free ?pos)
	(under ?pos1 ?pos2)
	(right ?pos1 ?pos2)
	(small ?block)
	(medium ?block)
	(long ?block)
	(big ?block)
)

(:action move_small_up
:parameters (?free_pos ?old_pos ?s)
:precondition (and (free ?free_pos)
					(at ?s ?old_pos)
					(under ?old_pos ?free_pos)
					(small ?s))
:effect (and (free ?old_pos)
			 (at ?s ?free_pos)
			 (not (free ?free_pos))
			 (not (at ?s ?old_pos))))
(:action move_small_down
:parameters (?free_pos ?old_pos ?s)
:precondition (and (free ?free_pos)
					(at ?s ?old_pos)
					(under ?free_pos ?old_pos)
					(small ?s))
:effect (and (free ?old_pos)
			 (at ?s ?free_pos)
			 (not (free ?free_pos))
			 (not (at ?s ?old_pos))))
(:action move_small_right 
:parameters (?free_pos ?old_pos ?s)
:precondition (and (free ?free_pos)
				    (at ?s ?old_pos)
				    (right ?free_pos ?old_pos)
				    (small ?s))
:effect (and (free ?old_pos)
			 (at ?s ?free_pos)
			 (not (free ?free_pos))
			 (not (at ?s ?old_pos))))

(:action move_small_left 
:parameters (?free_pos ?old_pos ?s)
:precondition (and (free ?free_pos)
					(at ?s ?old_pos)
					(right ?old_pos ?free_pos)
					(small ?s))
:effect (and (free ?old_pos)
			 (at ?s ?free_pos)
			 (not (free ?free_pos))
			 (not (at ?s ?old_pos))))
			 
(:action move_medium_up
:parameters (?free_pos ?old_pos1 ?old_pos2 ?m1)
:precondition (and (free ?free_pos)
				    (at ?m1 ?old_pos1)
				    (at ?m1 ?old_pos2)
				    (under ?old_pos2 ?old_pos1)
				    (under ?old_pos1 ?free_pos)
				    (medium ?m1) )
:effect (and (free ?old_pos2)
			 (at ?m1 ?free_pos)
			 (not (free ?free_pos))
			 (not (at ?m1 ?old_pos2)) ))
			 
(:action move_medium_down
:parameters (?free_pos ?old_pos1 ?old_pos2 ?m1)
:precondition (and (free ?free_pos)
				    (at ?m1 ?old_pos1)
				    (at ?m1 ?old_pos2)
				    (under ?old_pos2 ?old_pos1)
				    (under ?free_pos ?old_pos2)
				    (medium ?m1) )
:effect (and (free ?old_pos1)
			 (at ?m1 ?free_pos)
			 (not (free ?free_pos))
			 (not (at ?m1 ?old_pos1)) ))
			 
(:action move_medium_right
:parameters (?free_pos1 ?free_pos2 ?old_pos1 ?old_pos2 ?m1)
:precondition (and (free ?free_pos1)
					(free ?free_pos2)
					(at ?m1 ?old_pos1)
					(at ?m1 ?old_pos2)
					(under ?free_pos2 ?free_pos1)
					(under ?old_pos2 ?old_pos1)
					(right ?free_pos1 ?old_pos1)
					(right ?free_pos2 ?old_pos2)
					(medium ?m1))
:effect (and (free ?old_pos1)
			 (free ?old_pos2)
			 (at ?m1 ?free_pos1)
			 (at ?m1 ?free_pos2)
			 (not (free ?free_pos1))
			 (not (free ?free_pos2))
			 (not (at ?m1 ?old_pos1))
			 (not (at ?m1 ?old_pos2)) ))
			 
(:action move_medium_left
:parameters (?free_pos1 ?free_pos2 ?old_pos1 ?old_pos2 ?m1)
:precondition (and (free ?free_pos1)
					(free ?free_pos2)
					(at ?m1 ?old_pos1)
					(at ?m1 ?old_pos2)
					(under ?free_pos2 ?free_pos1)
					(under ?old_pos2 ?old_pos1)
					(right ?old_pos1 ?free_pos1)
					(right ?old_pos2 ?free_pos2)
					(medium ?m1))
:effect (and (free ?old_pos1)
			 (free ?old_pos2)
			 (at ?m1 ?free_pos1)
			 (at ?m1 ?free_pos2)
			 (not (free ?free_pos1))
			 (not (free ?free_pos2))
			 (not (at ?m1 ?old_pos1))
			 (not (at ?m1 ?old_pos2)) ))
			 
(:action move_long_up 
:parameters (?free_pos1 ?free_pos2 ?old_pos1 ?old_pos2 ?l1)
:precondition (and (free ?free_pos1)
					(free ?free_pos2)
					(at ?l1 ?old_pos1)
					(at ?l1 ?old_pos2)
					(under ?old_pos1 ?free_pos1)
					(under ?old_pos2 ?free_pos2)
					(right ?free_pos2 ?free_pos1)
					(right ?old_pos2 ?old_pos1)
					(long ?l1) ) 
:effect (and (free ?old_pos1)
			 (free ?old_pos2)
			 (at ?l1 ?free_pos1)
			 (at ?l1 ?free_pos2)
			 (not (free ?free_pos1))
			 (not (free ?free_pos2))
			 (not (at ?l1 ?old_pos1))
			 (not (at ?l1 ?old_pos2)) ))

(:action move_long_down
:parameters (?free_pos1 ?free_pos2 ?old_pos1 ?old_pos2 ?l1)
:precondition (and (free ?free_pos1)
					(free ?free_pos2)
					(at ?l1 ?old_pos1)
					(at ?l1 ?old_pos2)
					(under ?free_pos1 ?old_pos1)
					(under ?free_pos2 ?old_pos2)
					(right ?free_pos2 ?free_pos1)
					(right ?old_pos2 ?old_pos1)
					(long ?l1) ) 
:effect (and (free ?old_pos1)
			 (free ?old_pos2)
			 (at ?l1 ?free_pos1)
			 (at ?l1 ?free_pos2)
			 (not (free ?free_pos1))
			 (not (free ?free_pos2))
			 (not (at ?l1 ?old_pos1))
			 (not (at ?l1 ?old_pos2)) ))
(:action move_long_right 
:parameters (?free_pos ?old_pos1 ?old_pos2 ?l1)
:precondition (and (free ?free_pos)
					(at ?l1 ?old_pos1)
					(at ?l1 ?old_pos2) 
					(right ?old_pos2 ?old_pos1)
					(right ?free_pos ?old_pos2)
					(long ?l1) ) 
:effect (and (free ?old_pos1)
			 (at ?l1 ?free_pos)
			 (not (free ?free_pos))
			 (not (at ?l1 ?old_pos1)) ))
(:action move_long_left
:parameters (?free_pos ?old_pos1 ?old_pos2 ?l1)
:precondition (and (free ?free_pos)
					(at ?l1 ?old_pos1)
					(at ?l1 ?old_pos2) 
					(right ?old_pos2 ?old_pos1)
					(right ?old_pos1 ?free_pos)
					(long ?l1) ) 
:effect (and (free ?old_pos2)
			 (at ?l1 ?free_pos)
			 (not (free ?free_pos))
			 (not (at ?l1 ?old_pos2)) ))
					
(:action move_big_up 
:parameters (?free_pos1 ?free_pos2 ?old_pos1 ?old_pos2
			 ?old_pos3 ?old_pos4 ?b1)
:precondition (and (free ?free_pos1)
					(free ?free_pos2)
					(at ?b1 ?old_pos1)
					(at ?b1 ?old_pos2)
					(at ?b1 ?old_pos3)
					(at ?b1 ?old_pos4)
					(under ?old_pos3 ?old_pos1)
					(under ?old_pos4 ?old_pos2)
					(under ?old_pos1 ?free_pos1)
					(under ?old_pos2 ?free_pos2)
					(right ?old_pos2 ?old_pos1)
					(right ?old_pos4 ?old_pos3)
					(right ?free_pos2 ?free_pos1) 
					(big ?b1))
:effect (and (free ?old_pos3)
			 (free ?old_pos4)
			 (at ?b1 ?free_pos1)
			 (at ?b1 ?free_pos2)
			 (not (free ?free_pos1))
			 (not (free ?free_pos2))
			 (not (at ?b1 ?old_pos3))
			 (not (at ?b1 ?old_pos4)) ))
(:action move_big_down
:parameters (?free_pos1 ?free_pos2 ?old_pos1 ?old_pos2
			 ?old_pos3 ?old_pos4 ?b1)
:precondition (and (free ?free_pos1)
					(free ?free_pos2)
					(at ?b1 ?old_pos1)
					(at ?b1 ?old_pos2)
					(at ?b1 ?old_pos3)
					(at ?b1 ?old_pos4)
					(under ?old_pos3 ?old_pos1)
					(under ?old_pos4 ?old_pos2)
					(under ?free_pos1 ?old_pos3)
					(under ?free_pos2 ?old_pos4)
					(right ?old_pos2 ?old_pos1)
					(right ?old_pos4 ?old_pos3)
					(right ?free_pos2 ?free_pos1)
					(big ?b1))
:effect (and (free ?old_pos1)
			 (free ?old_pos2)
			 (at ?b1 ?free_pos1)
			 (at ?b1 ?free_pos2)
			 (not (free ?free_pos1))
			 (not (free ?free_pos2))
			 (not (at ?b1 ?old_pos1))
			 (not (at ?b1 ?old_pos2)) ))
(:action move_big_right
:parameters (?free_pos1 ?free_pos2 ?old_pos1 ?old_pos2
			 ?old_pos3 ?old_pos4 ?b1)
:precondition (and (free ?free_pos1)
					(free ?free_pos2)
					(at ?b1 ?old_pos1)
					(at ?b1 ?old_pos2)
					(at ?b1 ?old_pos3)
					(at ?b1 ?old_pos4)
					(under ?old_pos3 ?old_pos1)
					(under ?old_pos4 ?old_pos2)
					(under ?free_pos2 ?free_pos1)			
					(right ?old_pos2 ?old_pos1)
					(right ?old_pos4 ?old_pos3)
					(right ?free_pos1 ?old_pos2)
					(right ?free_pos2 ?old_pos4)
					(big ?b1))
:effect (and (free ?old_pos1)
			 (free ?old_pos3)
			 (at ?b1 ?free_pos1)
			 (at ?b1 ?free_pos2)
			 (not (free ?free_pos1))
			 (not (free ?free_pos2))
			 (not (at ?b1 ?old_pos1))
			 (not (at ?b1 ?old_pos3)) ))
(:action move_big_left
:parameters (?free_pos1 ?free_pos2 ?old_pos1 ?old_pos2
			 ?old_pos3 ?old_pos4 ?b1)
:precondition (and (free ?free_pos1)
					(free ?free_pos2)
					(at ?b1 ?old_pos1)
					(at ?b1 ?old_pos2)
					(at ?b1 ?old_pos3)
					(at ?b1 ?old_pos4)
					(under ?old_pos3 ?old_pos1)
					(under ?old_pos4 ?old_pos2)
					(under ?free_pos2 ?free_pos1)			
					(right ?old_pos2 ?old_pos1)
					(right ?old_pos4 ?old_pos3)
					(right ?old_pos1 ?free_pos1)
					(right ?old_pos3 ?free_pos2)
					(big ?b1))
:effect (and (free ?old_pos2)
			 (free ?old_pos4)
			 (at ?b1 ?free_pos1)
			 (at ?b1 ?free_pos2)
			 (not (free ?free_pos1))
			 (not (free ?free_pos2))
			 (not (at ?b1 ?old_pos2))
			 (not (at ?b1 ?old_pos4)) ))			 
)